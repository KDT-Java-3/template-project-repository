plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.ynv'
version = '0.0.1-SNAPSHOT'
description = 'ecommerce'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    implementation 'org.springframework.boot:spring-boot-starter-undertow'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'

    implementation 'org.mapstruct:mapstruct:1.5.5.Final'

    runtimeOnly 'com.mysql:mysql-connector-j'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    compileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

tasks.named('test') {
    useJUnitPlatform()
}

def generatedDir = layout.buildDirectory.dir("generated/querydsl")

tasks.register('generateQuerydsl', JavaCompile) {
    source = sourceSets.main.java
    classpath = configurations.compileClasspath + configurations.annotationProcessor
    options.annotationProcessorPath = configurations.annotationProcessor
    destinationDirectory = generatedDir.get().asFile
    options.compilerArgs = [
            '-proc:only',
            '-processor', 'com.querydsl.apt.jpa.JPAAnnotationProcessor'
    ]
}

sourceSets {
    main {
        java {
            srcDirs += generatedDir
        }
    }
}

tasks.named('compileJava') {
    dependsOn(tasks.named('generateQuerydsl'))
}